buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven { url "https://jitpack.io" } // for holo-graph
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "org.codeisland.biketrack"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 2
        versionName "1.1"
    }

    signingConfigs {
        // Load the configuration for signing from a properties file, which is NOT shared.
        def propsFile = rootProject.file("signing.properties")
        if (!propsFile.exists()){
            // Signing the APK is necessary, because the Google Maps API doesn't work otherwise.
            throw new InvalidUserDataException("\"signing.properties\" File with signing configuration was not found.")
        }
        def props = new Properties()
        props.load(new FileInputStream(propsFile))

        mapsSupport {
            storeFile rootProject.file(props.keystore)
            storePassword props.storePass
            keyAlias props.alias
            keyPassword props.keyPass
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.mapsSupport
            debuggable true
        }
        release {
            signingConfig signingConfigs.mapsSupport
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Inject the replacement-value for all Api Keys into the build-types.
    // We keep the API Keys separate so we don't have to share them in the public repository.
    def propsFile = rootProject.file("apikeys.properties")
    if (!propsFile.exists()){
        throw new InvalidUserDataException("\"apikeys.properties\" File with API Keys not found!");
    }
    def props = new Properties();
    props.load(new FileInputStream(propsFile))
    buildTypes.all{ buildType ->
        buildType.manifestPlaceholders.put("mapsApiKey", props.mapsApiKey)
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.github.Androguide:HoloGraphLibrary:5562473'
}
